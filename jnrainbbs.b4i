Version=1.8
IconFile=
NumberOfModules=3
Module1=HttpJob
Module2=HttpUtils2Service
Module3=CustomListView
Build1=jnrain,org.xulihang.jnrain
NumberOfFiles=8
File1=leftpage.bil
File2=login.bil
File3=main.bil
File4=newpost.bil
File5=setting.bil
File6=thread.bil
File7=uploadpage.bil
File8=wuqu.mp3
NumberOfLibraries=7
Library1=icore
Library2=ijson
Library3=ihud
Library4=ihttp
Library5=istringutils
Library6=isidemenu
Library7=iphone
@EndOfDesignText@
'Code module
#Region  Project Attributes 
	#ApplicationLabel: 江南听雨BBS
	#Version: 1.0.0 
	'Orientation possible values: Portrait, LandscapeLeft, LandscapeRight and PortraitUpsideDown
	#iPhoneOrientations: Portrait, LandscapeLeft, LandscapeRight
	#iPadOrientations: Portrait, LandscapeLeft, LandscapeRight, PortraitUpsideDown
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'Public variables can be accessed from all modules.
	Public App As Application
	Public NavControl As NavigationController
	Private Page1 As Page
	Private Page2 As Page
	Private SettingPage As Page
	Private LoginPage As Page
	Private PostPage As Page
	Private WebView1 As WebView
	Private currentposition=1 As Int  '目前位置：1未加载大区列表，2为大区列表，3为版面列表
	Private currentpage=1 As Int  '目前帖子列表页面
	Private currentpageOfThread=1 As Int  '目前帖子页面
	Private parameter As String '查看版面thread列表的参数
	Private TURL As String '查看thread内容的参数，方便换页设为全局变量
	Private boardName(35) As String
	Private threadID As String '帖子ID
	Private isNewThread=True As Boolean 
	Private imageURL As String
	Private hd As HUD
	Private clv1 As CustomListView
	Private clv2 As CustomListView
	Private ActionSheet1 As ActionSheet
	Private ActionSheet2 As ActionSheet
	Private ActionSheet3 As ActionSheet
	Private ActionSheet4 As ActionSheet
	Private smc As SideMenuController
	Private Switch1 As Switch
	Private Button1 As Button
	Private TextField1 As TextField
	Private TextField2 As TextField
	Private loginLabel As Label
	Private InsertEm As Button
	Private Picker1 As Picker
	Private GifWV As WebView
	Private ContentTV As TextView
	Private TitleTF As TextField
	Private Upload As Button
	Private uploadWV As WebView
	Private aboutme As Label
	Private duration1 As Long
	Dim MP As MediaPlayer
	Private Switch2 As Switch
End Sub

Private Sub Application_Start (Nav As NavigationController)
	'NavControl = Nav
	Dim nc As NavigationController
    nc.Initialize("nc")
    NavControl = nc
	Page1.Initialize("Page1")
	Page1.RootPanel.Color = Colors.White
	Page1.RootPanel.LoadLayout("main")
	Page1.Title = "江南听雨"
	Page2.Initialize("Page2")
	Page2.RootPanel.Color = Colors.White
	Page2.RootPanel.LoadLayout("thread")
	Page2.Title = "帖子内容"
	SettingPage.Initialize("SettingPage")
	SettingPage.RootPanel.Color = Colors.White
	SettingPage.RootPanel.LoadLayout("setting")
	SettingPage.Title = "设置"
	LoginPage.Initialize("LoginPage")
	LoginPage.RootPanel.Color = Colors.White
	LoginPage.RootPanel.LoadLayout("login")
	LoginPage.Title = "登录"
	PostPage.Initialize("PostPage")
	PostPage.RootPanel.Color = Colors.White
	PostPage.RootPanel.LoadLayout("newpost")
	Dim leftpage As Page
	leftpage.Initialize("leftpage")
	leftpage.RootPanel.Color = Colors.Red
	leftpage.RootPanel.LoadLayout("leftpage")
	ActionSheet1.Initialize("actionsheet1","操作", "Cancel", "", Array As String("十大热帖","版块列表","发帖"))
	ActionSheet2.Initialize("actionsheet2","帐户", "Cancel", "", Array As String("登录","注销"))
	ActionSheet3.Initialize("actionsheet3","帖子", "Cancel", "", Array As String("上一页","下一页","回帖"))
	ActionSheet4.Initialize("actionsheet4","保存图片", "Cancel", "", Array As String("保存"))
	'NavControl.ShowPage(Page1)
	'NavControl.NavigationBarVisible=False
	clv1.Initialize(Me, "clv1",100%X)
	clv2.Initialize(Me, "clv2",100%X)
	Page1.RootPanel.AddView(clv1.AsView, 0, 0, 100%x, 100%y-40dip)
	leftpage.RootPanel.AddView(clv2.AsView,0,80dip,100%x,100%y-80dip)
	Log(File.DirAssets)
	smc.Initialize(leftpage, nc, Null)
	smc.LeftMenuMaxWidth=40%x
    App.KeyController = smc
    nc.ShowPage(Page1)
    Page1.TopLeftButtons = Array(smc.CreateBarButton("left"))
	loadclv2
	loadTopX
	If File.Exists(File.DirDocuments,".login") Then
	    If File.ReadString(File.DirDocuments,".login")=0 Then
		    loginLabel.Text="已登录"
		End If
	End If
	GifWV.UserInteractionEnabled=False
	If File.Exists(File.DirDocuments,".enablezoom") Then
	    If File.ReadString(File.DirDocuments,".enablezoom")=True Then
		    WebView1.ScaleToFit=True
		End If
	End If
	'Log(File.DirDocuments)
	'Log(File.DirLibrary)
	'Log(File.DirTemp)
End Sub

Private Sub Page1_Resize(Width As Int, Height As Int)
	
End Sub

Private Sub Application_Background
	
End Sub

Sub loadclv2
    clv2.AddTextItem("十大热帖",0)
	clv2.AddTextItem("版块列表",1)
	clv2.AddTextItem("设置",2)
End Sub

Sub JobDone (job As HttpJob)
	Log("JobName = " & job.JobName & ", Success = " & job.Success)
	If job.Success = True Then
		Select job.JobName
			Case "get"
				loadTopten(job.GetString)
			Case "getsec"
			    loadSec(job.GetString)
			Case "getThread"
			    loadThread(job.GetString,False)
			Case "getThread2"
			    loadThread(job.GetString,True)
			Case "getcontent"
			    loadcontent(job.GetString)
			Case "userlogin"
			    loginDone(job.GetString)
			Case "userlogout"
			    logoutDone(job.GetString)
			Case "newpost"
			    postDone(job.GetString)
			Case "getimage"
			    getimageDone(job.GetBitmap)
		End Select
	Else
		Log("Error: " & job.ErrorMessage)
		hd.ToastMessageShow("Error: " & job.ErrorMessage, True)
	End If
	hd.ProgressDialogHide
	job.Release
End Sub

Sub LoadSection
    currentposition=2
    'WebView1.Visible=False
    'clv1.AsView.Visible=True
	clv1.Clear
    clv1.AddTextItem("站务系统",0)
	clv1.AddTextItem("江南大学",1)
	clv1.AddTextItem("文化艺术",2)
	clv1.AddTextItem("电脑技术",3)
	clv1.AddTextItem("学术科学",4)
	clv1.AddTextItem("菁菁校园",5)
	clv1.AddTextItem("知性感性",6)
	clv1.AddTextItem("休闲娱乐",7)
	clv1.AddTextItem("社团群体",8)
	clv1.AddTextItem("校务邮箱",9)
	clv1.AddTextItem("服务专区",10)
End Sub

Sub clv1_ItemClick (position As Int, Value As Object)
    If currentposition=2 Then
        Dim getsec As HttpJob
	    getsec.Initialize("getsec",Me)
	    getsec.Download("http://bbs.jiangnan.edu.cn/rainstyle/boards_json.php?sec="&Value)
		Log(Value)
	Else If currentposition=3 Then
	    parameter=Value
	    Dim getThread As HttpJob
		getThread.Initialize("getThread",Me)
	    getThread.Download("http://bbs.jiangnan.edu.cn/rainstyle/board_json.php?name="&Value&"&page=1")
		Log(Value)
	Else If currentposition=4 Then
	    threadID=Value
	    If Value="next" Then
		    NextPage
		Else If Value="previous" Then
		    PreviousPage
		Else
	        TURL="http://bbs.jiangnan.edu.cn/rainstyle/thread_json.php?boardName="&boardName(position)&"&ID="&Value
	        Dim getcontent As HttpJob
		    getcontent.Initialize("getcontent",Me)
	        getcontent.Download(TURL)
		    Log(Value)
		End If
	Else If currentposition=0 Then
	    boardName(20)=boardName(position)
	    threadID=Value '让由十大进入的帖子时也能回帖
	    TURL="http://bbs.jiangnan.edu.cn/rainstyle/thread_json.php?boardName="&boardName(position)&"&ID="&Value
	    Dim getcontent As HttpJob
		getcontent.Initialize("getcontent",Me)
	    getcontent.Download(TURL)
	End If
End Sub

Sub clv2_ItemClick (position As Int, Value As Object)
    Select position
	    Case 0
		    smc.CloseMenu
		    currentpage=1
		    loadTopX
		Case 1
		    smc.CloseMenu
		    currentpage=1
		    LoadSection
		Case 2
		    smc.CloseMenu
			If File.Exists(File.DirDocuments,".loadphoto") Then
			    Switch1.Value=File.ReadString(File.DirDocuments,".loadphoto")
			End If
			If File.Exists(File.DirDocuments,".enablezoom") Then
			    Switch2.Value=File.ReadString(File.DirDocuments,".enablezoom")
			End If
		    NavControl.ShowPage(SettingPage)
	End Select
End Sub
Sub loadSec(jsonresult As String)
    currentposition=3
    clv1.Clear
    Dim JSON As JSONParser
    'JSON.Initialize(File.ReadString(File.DirAssets, "out.json")) 'Read the text from a file.
	JSON.Initialize(jsonresult)
	Dim map1 As Map
    map1 = JSON.Nextobject
    Dim list1 As List
	list1= map1.get("boards")
	Log("size:"&list1.Size)
	For i =0 To list1.Size-1
	    Dim map2 As Map
        map2 = list1.Get(i)
		Dim name As String
		name = map2.Get("name")
		Dim id As String
		id = map2.Get("id")
		clv1.AddTextItem(name,id)
		Log(id)
	Next
End Sub

Sub loadThread(jsonresult As String, showPreviousPage As Boolean)
    currentposition=4
	currentpageOfThread=1
    clv1.Clear
	If showPreviousPage=True Then
	    clv1.AddTextItem("上一页","previous")
	End If
    Dim JSON As JSONParser
	JSON.Initialize(jsonresult)
	Dim map1 As Map
    map1 = JSON.Nextobject
    Dim list1 As List
	list1= map1.get("posts")
	Log("size:"&list1.Size)
	For i =0 To list1.Size-1
	    Dim map2 As Map
        map2 = list1.Get(i)
		Dim name As String
		name = map2.Get("title")
		Dim id As String
		id = map2.Get("id")
		boardName(i) = map2.Get("board")
		clv1.AddTextItem(name,id)
		If i=list1.Size-1 Then
		    clv1.AddTextItem("下一页","next")
		End If
	Next
End Sub

Sub loadcontent(jsonresult As String)
    'clv1.Clear
    'currentposition=5
	'currentpage=1
	NavControl.ShowPage(Page2)
    'ListView1.Clear
	'clv1.AsView.Visible=False
    'WebView1.Visible=True
    Dim JSON As JSONParser
    JSON.Initialize(jsonresult)
    Dim map1 As Map
    map1 = JSON.Nextobject
    Log(map1.Size)
    'm = map1.GetKeyAt(12)
	Dim posts As List
	Dim map2 As Map
	Dim content1 As String 
	content1="<link rel=" & Chr(34) &"stylesheet" & Chr(34) &" type=" & Chr(34) & "text/css" & Chr(34) &" href=" & Chr(34) &"File:///"&File.DirAssets&"/mystyle.css" & Chr(34) &" />"
	'"<img src=File:///"&File.DirAssets&"/icon-60.png>"
	'"<script src="&File.DirAssets&"'/jsinterface.js'  language='javascript'></script>"
	posts= map1.get("posts") '根据Key的名称
	For i =0 To posts.Size-1
        map2=posts.Get(i)
		If i>0 Then
		content1=content1 & "<br />" &"-------------------"& "<br />" & map2.Get("content")
		Else
		content1=content1 & map2.Get("content")
		End If
	Next
	content1=content1.Replace("js/xheditor-1.1.9/xheditor_emot/default/","File:///"&File.DirAssets&"/")
	'"http://bbs.jiangnan.edu.cn/wForum/js/xheditor-1.1.9/xheditor_emot/default/"
	content1=content1.Replace("emot/jn/","File:///"&File.DirAssets&"/")
	'"http://bbs.jiangnan.edu.cn/wForum/emot/jn/")
	'content1=content1.Replace("audioplayer/audio-player.js",File.DirAssets&"/audio-player.js")
	'content1=content1.Replace("audioplayer/player.swf",File.DirAssets&"/player.swf")
	If File.Exists(File.DirDocuments,".loadphoto") Then
	    If File.ReadString(File.DirDocuments,".loadphoto")=True Then
	        content1=content1.Replace("/attachments/","http://bbs.jiangnan.edu.cn/attachments/")
		End If
	End If
	'content1=content1.Replace("<a href=","<a href=" & Chr(34) &"javascript:void(0)" & Chr(34) &" onclick=getData(")
	'content1=content1.Replace(">下载",")>下载")
	Log(content1)
	Log("File:///"&File.DirAssets&"/")
	WebView1.LoadHtml(content1)
End Sub


Sub loadTopX
    currentpageOfThread=1
    Dim get As HttpJob
	get.Initialize("get",Me)
	get.Download("http://bbs.jiangnan.edu.cn/rainstyle/topten_json.php")
End Sub

Sub loadTopten(jsonresult As String)
    currentposition=0
    clv1.Clear
    Dim JSON As JSONParser
	JSON.Initialize(jsonresult)
	Dim map1 As Map
    map1 = JSON.Nextobject
    Dim list1 As List
	list1= map1.get("posts")
	Log("size:"&list1.Size)
	For i =0 To list1.Size-1
	    Dim map2 As Map
        map2 = list1.Get(i)
		Dim name As String
		name = map2.Get("title")
		Dim id As String
		id = map2.Get("id")

		boardName(i) = map2.Get("board")
		clv1.AddTextItem(name,id)
	Next
End Sub

'查看帖子列表的下一页
Sub NextPage
    Log(currentpage)
	Log(parameter)
	currentpage=currentpage+1
	Dim getThread2 As HttpJob
	getThread2.Initialize("getThread2",Me)
	getThread2.Download("http://bbs.jiangnan.edu.cn/rainstyle/board_json.php?name="&parameter&"&page="&currentpage)
End Sub

'查看帖子列表的上一页
Sub PreviousPage
    Log(currentpage)
	Log(parameter)
	If currentpage=1 Then
	    hd.ToastMessageShow("已经是第一页了",False)
	Else
	    currentpage=currentpage-1
	End If
	Dim getThread2 As HttpJob
	getThread2.Initialize("getThread2",Me)
	getThread2.Download("http://bbs.jiangnan.edu.cn/rainstyle/board_json.php?name="&parameter&"&page="&currentpage)
End Sub

' Page1 TopRightBar buttons event routine
Private Sub Page1_BarButtonClick (Tag As String)
	Select Tag
		Case "left"
		    If smc.Tag="opened" Then
			    smc.Tag="closed"
			    smc.CloseMenu
			Else
			    smc.Tag="opened" 
			    smc.OpenLeftMenu
			End If
		Case "Action"
			ActionSheet1.Show(Page1.RootPanel)
	End Select
End Sub

' Page2 TopRightBar buttons event routine
Private Sub Page2_BarButtonClick (Tag As String)
    ActionSheet3.Show(Page1.RootPanel)
End Sub

Private Sub PostPage_BarButtonClick (Tag As String)
    If isNewThread=False Then
	    hd.ProgressDialogShow("回帖中")
	    Dim param As String
	    param="subject="&TitleTF.Text&"&Content="&ContentTV.Text&"&board="&boardName(20)&"&signature=1&ID="&threadID&"&groupID="&threadID&"&reID="&threadID
	    Log(param)
	    Dim newPost As HttpJob
	    newPost.Initialize("newpost",Me)
	    newPost.PostString("http://bbs.jiangnan.edu.cn/rainstyle/apipost.php",param)
	Else
        hd.ProgressDialogShow("发帖中")
	    Dim param As String
	    param="subject="&TitleTF.Text&"&Content="&ContentTV.Text&"&board="&boardName(20)&"&signature=1&ID=&groupID=&reID=0"
	    Log(param)
	    Dim newPost As HttpJob
	    newPost.Initialize("newpost",Me)
	    newPost.PostString("http://bbs.jiangnan.edu.cn/rainstyle/apipost.php",param)
	End If
End Sub

' ActionSheet Pages event routine
Private Sub actionsheet1_Click(Item As String)
	Select Item
	Case "十大热帖"
		currentpage=1
	    loadTopX
	Case "版块列表"
		currentpage=1
	    LoadSection
	Case "发帖"
	    TitleTF.Text=""
	    ContentTV.Text=""
	    If currentposition=4 Then
		    Picker1.SetItems(0,Array As String("01","02","03","04","05","06","07","08","09","10","11","12","13","14","15","16","17","18"))
		    isNewThread=True
			NavControl.ShowPage(PostPage)
		Else
		    hd.ToastMessageShow("请在相应版块位置操作",False)
		End If
	End Select
End Sub

' ActionSheet Pages event routine
Private Sub actionsheet2_Click(Item As String)
	Select Item
	Case "登录"
	    If File.Exists(File.DirDocuments,".login") Then
		    If File.ReadString(File.DirDocuments,".login")=0 Then
			    hd.ToastMessageShow("你已登录",False)
			Else
			    smc.CloseMenu
	            NavControl.ShowPage(LoginPage)
			End If
		Else 
		    smc.CloseMenu
		    NavControl.ShowPage(LoginPage)
		End If
	Case "注销"
	    If File.Exists(File.DirDocuments,".login") Then
		    If File.ReadString(File.DirDocuments,".login")=0 Then
			    hd.ProgressDialogShow("注销中")
			    Dim userlogout As HttpJob
	            userlogout.Initialize("userlogout",Me)
	            userlogout.PostString("http://bbs.jiangnan.edu.cn/rainstyle/apilogout.php","")
			Else
	            hd.ToastMessageShow("你未登录",False)
			End If
		Else
		    hd.ToastMessageShow("你未登录过",False)
		End If
	End Select
End Sub

' ActionSheet Pages event routine
Private Sub actionsheet3_Click(Item As String)
	Select Item
	Case "上一页"
	    If currentpageOfThread=1 Then
		    hd.ToastMessageShow("已经是第一页了",False)
		Else
		    currentpageOfThread=currentpageOfThread-1
			TURL=TURL&"&page="&currentpageOfThread
	        Dim getcontent As HttpJob
            getcontent.Initialize("getcontent",Me)
	        getcontent.Download(TURL)
		End If
	Case "下一页"
        currentpageOfThread=currentpageOfThread+1
		TURL=TURL&"&page="&currentpageOfThread
	    Dim getcontent As HttpJob
        getcontent.Initialize("getcontent",Me)
	    getcontent.Download(TURL)
	Case "回帖"
	    TitleTF.Text="Reply"
	    ContentTV.Text=""
	    Picker1.SetItems(0,Array As String("01","02","03","04","05","06","07","08","09","10","11","12","13","14","15","16","17","18"))
	    isNewThread=False
        NavControl.ShowPage(PostPage)
	End Select
    
End Sub

Sub Switch1_ValueChanged (Value As Boolean)
	File.WriteString(File.DirDocuments,".loadphoto",Value)
	Log(File.ReadString(File.DirDocuments,".loadphoto"))
End Sub

Sub WebView1_OverrideUrl (URL As String) As Boolean
    If URL.Contains("mp3")=True Then
	    hd.ToastMessageShow("将用系统播放器播放",False)
	Else If URL.Contains("jpg") OR URL.Contains("png") OR URL.Contains("gif") OR URL.Contains("bmp") Then
	    imageURL=URL
		ActionSheet4.Show(Page2.RootPanel)
	    'Msgbox2("saveImage","保存图片","保存到相册查看吗？",Array("好的","不用")) 莫名会出现两次，放弃
		WebView1.LoadHtml("请选择操作")
	Else
        WebView1.LoadHtml("目前不支持点击")
	    hd.ToastMessageShow("目前不支持点击",False)
	End If
End Sub

Private Sub actionsheet4_Click(Item As String)
    Select Item
        Case "保存"
		    Dim getimage As HttpJob
			getimage.Initialize("getimage",Me)
			getimage.Download(imageURL)
			ActionSheet4.Dismiss
		    hd.ProgressDialogShow("下载中")
    End Select
End Sub

Sub getimageDone(image As Bitmap)
    hd.ToastMessageShow("下载完成",False)
    Dim plib As Phone
	plib.Vibrate
	plib.AddImageToAlbum(image)
End Sub

'登录按钮
Sub Button1_Click
    If TextField1.Text="guest" Then
	    Msgbox("匿名用户不能登录","错误")
	Else
        hd.ProgressDialogShow("登录中")
	    Dim param As String
	    param="uid="&TextField1.Text&"&psw="&TextField2.Text
	    Log(param)
	    Dim userlogin As HttpJob
	    userlogin.Initialize("userlogin",Me)
	    userlogin.PostString("http://bbs.jiangnan.edu.cn/rainstyle/apilogin.php",param)
	End If
End Sub

Sub loginDone(getjson As String)
    Log(getjson)
    Dim JSON As JSONParser
	JSON.Initialize(getjson)
	Dim map1 As Map
    map1 = JSON.Nextobject
    Dim status As Int
	status= map1.get("status")
	Log(status)
	If  status=0 Then
		hd.ToastMessageShow("登录成功",True)
		loginLabel.Text="已登录"
		NavControl.ShowPage(Page1)
		File.WriteString(File.DirDocuments,".login","0")
	Else If status=1 Then 
	    hd.ToastMessageShow("用户不存在或密码错误",True)
	Else If status=2 Then
	    hd.ToastMessageShow("用户名为空",True)
	Else If status=3 Then
	    hd.ToastMessageShow("已登录帐号过多",True)
	Else If status=4 Then
	    hd.ToastMessageShow("帐户被ban",True)
	Else If status=5 Then
	    hd.ToastMessageShow("IP被ban",True)
	Else If status=6 Then
	    hd.ToastMessageShow("请勿频繁登录",True)
	Else If status=7 Then
	    hd.ToastMessageShow("系统在线人数达到极限",True)	
	End If
End Sub

Sub logoutDone(getjson As String)
    Log(getjson)
    Dim JSON As JSONParser
	JSON.Initialize(getjson)
	Dim map1 As Map
    map1 = JSON.Nextobject
    Dim status As Int
	status= map1.get("status")
	Log(status)
	If  status=0 Then
		hd.ToastMessageShow("注销成功",True)
		File.WriteString(File.DirDocuments,".login","1")
		loginLabel.Text="未登录"
    Else 
	    hd.ToastMessageShow("注销失败",True)
	End If
End Sub

Sub postDone(getjson As String)
    Log(getjson)
    Dim JSON As JSONParser
	JSON.Initialize(getjson)
	Dim map1 As Map
    map1 = JSON.Nextobject
    Dim status As Int
	status= map1.get("status")
	Log(status)
	If  status=0 Then
	    NavControl.RemoveCurrentPage
		hd.ToastMessageShow("发帖成功",True)
    Else If status=1 Then
	    hd.ToastMessageShow("游客不能发帖",True)
	Else
	    hd.ToastMessageShow("发帖失败",True)
	End If
End Sub

Sub loginLabel_Click
    smc.CloseMenu
	ActionSheet2.Show(Page1.RootPanel)
End Sub

Sub Picker1_ItemSelected (Column As Int, Row As Int)
    Log(Picker1.GetSelectedRow(0))
	Log(Picker1.GetSelectedItem(0))
	Dim gifpath As String
	gifpath="File:///"&File.DirAssets&"/em"&Picker1.GetSelectedItem(0)&".gif"
    GifWV.LoadHtml("<html><body><img src='"&gifpath&"'/></body></html>")
End Sub

Sub InsertEm_Click
	ContentTV.Text=ContentTV.Text&"[em"&Picker1.GetSelectedItem(0)&"]"
End Sub

Sub Upload_Click
    Dim uploadpage As Page
	uploadpage.Initialize("uploadpage")
	uploadpage.RootPanel.Color = Colors.Red
	uploadpage.RootPanel.LoadLayout("uploadpage")
	NavControl.ShowPage(uploadpage)
	uploadWV.LoadUrl("http://bbs.jiangnan.edu.cn/attachments/upload.php")
End Sub

Sub uploadWV_PageFinished (Success As Boolean, Url As String)
	Dim result As String
	Dim no As NativeObject = uploadWV
	result=no.RunMethod("stringByEvaluatingJavaScriptFromString:", Array("document.documentElement.innerHTML")).AsString
	If result.Contains("pic") Then
	    Dim ubound As Int
		Dim lbound As Int
		ubound=result.IndexOf("[pic")
		lbound=result.IndexOf("</script>")-3
		Log(ubound)
		Log(lbound)
		result=result.SubString2(ubound,lbound)
		Log(result)
	    ContentTV.Text=ContentTV.Text&result
	End If
End Sub
Sub aboutme_Click
	aboutme.Tag=aboutme.Tag+1
	hd.ToastMessageShow(aboutme.Tag,False)
	If aboutme.Tag=2 Then
	    MP.Initialize(File.DirAssets,"wuqu.mp3","MP")
	    MP.Play
		duration1=DateTime.Now
	End If
	If aboutme.Tag=3 Then
	    duration1=DateTime.Now-duration1
		If duration1<MP.Duration Then
		    aboutme.Tag=0
		End If
	End If
	If aboutme.Tag=5 Then
	    hd.ToastMessageShow("你中奖了",False)
	End If
End Sub

Sub Switch2_ValueChanged (Value As Boolean)
	File.WriteString(File.DirDocuments,".enablezoom",Value)
	WebView1.ScaleToFit=Value
End Sub